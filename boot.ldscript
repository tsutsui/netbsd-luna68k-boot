/*	$NetBSD: boot.ldscript,v 1.2 2013/01/10 13:10:26 tsutsui Exp $	*/

OUTPUT_FORMAT("a.out-m68k-netbsd")
OUTPUT_ARCH(m68k)
ENTRY(start)
SECTIONS
{
  . = 0x00700000;

  /* Read-only sections, merged into text segment: */
  .text :
  {
    *(.text)
    *(.text.*)
    *(.rodata) *(.rodata.*)
    . = ALIGN(4);
  } =0
  PROVIDE (__etext = .);
  PROVIDE (_etext = .);
  PROVIDE (etext = .);
  .data :
  {
    __data_start = . ;
    *(.data)
    *(.data.*)
    *(.sdata)
    *(.sdata.*)
  }
  _edata = .;
  PROVIDE (edata = .);
  __bss_start = .;
  __bss_start__ = .;
  .bss       :
  {
   *(.dynbss)
   *(.bss)
   *(.bss.*)
   *(COMMON)
   /* Align here to ensure that the .bss section occupies space up to
      _end.  Align after .bss to ensure correct alignment even if the
      .bss section disappears because there are no input sections.  */
/*   . = ALIGN(32 / 8); */
  }
/*  . = ALIGN(32 / 8);*/
  _end = .;
  _bss_end__ = . ; __bss_end__ = . ; __end__ = . ;
  PROVIDE (end = .);

  /DISCARD/ : { *(.ident) *(.stab) *(.stabstr) *(.comment) }
}
